apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'kotlin-android'

def static releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.ljstudio.android.loveday"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 62
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../loveday.jks")
            keyAlias "loveday"
            storePassword "lj123456"
            keyPassword "lj123456789"
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"

            versionNameSuffix "-debug"
            minifyEnabled false
//            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"

//            versionNameSuffix "-release"
            minifyEnabled false
//            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            variant.outputs.all { output ->
//            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    //  apk名称为 LoveDay-V1.0.0_2015-12-31_online.apk
                    def fileName = outputFile.name.replace("app", "LoveDay-V${versionName}_${releaseTime()}")
//                    outputFileName = new File(outputFile.parent, fileName)
                    outputFileName = new File(fileName)

//                    def fileName = "LoveDay-V${versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
//                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }

    // Specifies a flavor dimension.
    flavorDimensions "channel"

    productFlavors {
        apk {
            manifestPlaceholders = [APK_CHANNEL: "apk"]

            buildConfigField "String", "DOMAIN_ADD", "\"https://online.domain.com/\""
            versionName getReleaseVersionName()

            dimension "channel"
        }

        online {
            manifestPlaceholders = [APK_CHANNEL: "online"]

            buildConfigField "String", "DOMAIN_ADD", "\"https://online.domain.com/\""
            versionName getReleaseVersionName()

            dimension "channel"
        }

        admin {
            manifestPlaceholders = [APK_CHANNEL: "admin"]

            buildConfigField "String", "DOMAIN_ADD", "\"https://admin.domain.com/\""
            versionName getDebugVersionName()

            dimension "channel"
        }

        atest {
            manifestPlaceholders = [APK_CHANNEL: "atest"]

            buildConfigField "String", "DOMAIN_ADD", "\"https://offline.domain.com/\""
            versionName getDebugVersionName()

            dimension "channel"
        }
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

greendao {
    schemaVersion 2
    daoPackage 'com.ljstudio.android.loveday.greendao'
    targetGenDir 'src/main/java'
}

ext {
    supportlib_version = "27.1.0"
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "$supportlib_version"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'

    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:appcompat-v7:$supportlib_version"
    implementation "com.android.support:design:$supportlib_version"

    implementation 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    implementation 'com.github.bumptech.glide:glide:4.1.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.1.1'

    implementation 'org.greenrobot:greendao:3.2.2'

    implementation 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    implementation 'com.readystatesoftware.systembartint:systembartint:1.0.4'

    implementation 'com.github.flavienlaurent.datetimepicker:library:0.0.2'

    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'com.google.code.gson:gson:2.8.1'

    implementation 'net.sourceforge.jexcelapi:jxl:2.6.12'
    // https://mvnrepository.com/artifact/net.sourceforge.jexcelapi/jxl
    implementation 'com.tapadoo.android:alerter:1.0.4'
    implementation 'com.yanzhikai:TextPathView:0.0.3'

    implementation 'com.zhy:okhttputils:2.6.2'

    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.15'
    implementation 'com.github.nekocode:TriangulationDrawable:1.1'
    implementation 'com.github.lzyzsd.randomcolor:library:1.0.0'
    implementation 'com.github.GrenderG:Toasty:1.2.8'
    implementation 'com.vdurmont:emoji-java:4.0.0'
//    implementation 'com.github.385841539:MarqueeView:1.0.0'

    implementation 'com.afollestad.material-dialogs:core:0.9.4.4'
    implementation 'com.afollestad.material-dialogs:commons:0.9.4.4'

    implementation "io.reactivex.rxjava2:rxjava:2.1.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

repositories {
    mavenCentral()
}



